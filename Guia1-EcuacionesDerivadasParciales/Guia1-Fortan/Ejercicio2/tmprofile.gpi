# primero cargo los datos externos
# el nombre del archivo 
input='TEMP.dat'
# necesio el numero de pasos temporales que hice
# nblocks=system(" awk 'BEGIN{RS=\"\\n\\n\"};END{print NR}'  ".input)
# Necesito el m√©todo aplicado
metod=system("grep -i method DATIN | awk '{ print $NF }' ")
# estadisticas del sistema
stats input nooutput
# necesito el valor del paso temporal
dt=system("grep -i dt DATIN | awk '{ print $NF }' ")
# el valor de dx
dx=system("grep -i dx DATIN | awk '{ print $NF }' ")
kx=system("grep -i kx DATIN | awk '{print $NF}' ")
lambda= kx*dt/(dx**2)
lambtitle=gprintf("%6.3f",lambda)
metitle=gprintf("%2.0f",metod)
if (metod == "1") metodlabel = " explicito " 
if (metod == "2") metodlabel = " implicito " 
if (metod == "3") metodlabel = " C-N "


set terminal pdf enhanced color
set output 'tmprofile_met'.metitle.'_LAM'.lambtitle.'.pdf'

set xtics font 'Arial,8'
set ytics font 'Arial,8'
set xlabel font 'Arial,12'

# plot for [ind=0:nblocks-1:nblocks/15] 'TEMP.dat' index ind u 2:3 w l lt 1 noti
set title 'Metodo '.metodlabel.' {/Symbol l} = '.lambtitle.', '.gprintf("%4.0f",STATS_blocks)
plot for [ind=0:STATS_blocks-1:(STATS_blocks-1)/15] 'TEMP.dat' index ind u 2:3 w l lt 1 noti

plot  'FLUX.dat' u 1:2  w l  ti 'flujo a la izquierda',\
      'FLUX.dat' u 1:3  w l   ti 'flujo a la derecha'
